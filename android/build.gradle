group 'com.fluttercandies.flutter_bdface_collect'
version '1.0'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

def libs = file("libs")

rootProject.allprojects {
    repositories {
        google()
        jcenter()
        flatDir { dirs libs }
    }
}

def flutterRoot = file("local.properties").with {
    if(!it.exists()) return null
    new Properties().with(true, p -> p.load(it.newReader()))["flutter.sdk"]
}
println("flutterRoot: $flutterRoot")

if(flutterRoot){
    //Let flutter complete the dependencies of android

    apply plugin: 'com.android.application'

    apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

    flutter {
        source '../..'
    }
}else{
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.has('compileSdkVersion')? rootProject.ext.compileSdkVersion: 31

    defaultConfig {
        minSdkVersion 22
    }

    sourceSets {
        main.jniLibs.srcDir 'libs'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    packagingOptions {
        merge 'res/values/values.xml'
        merge 'AndroidManifest.xml'
        merge 'R.txt'
        merge 'classes.jar'
    }
    buildTypes {
        release {
            consumerProguardFiles "proguard-rules.pro"
        }
    }
}


dependencies {
    implementation(name: "faceplatform-release-4.1.5", ext: "aar")
}
